apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId 'io.ribot.app'
        minSdkVersion 14
        targetSdkVersion 24
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
        versionCode 5000
        // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
        versionName '0.5.0'
        buildConfigField("String", "GOOGLE_API_SERVER_CLIENT_ID",
                "\"${ribotAppGoogleApiServerClientId}\"")
    }

    signingConfigs {
        // You must set up an environment var before release signing
        // Run: export RIBOT_APP_KEY={password}
        release {
            storeFile file("${ribotAppKeystoreReleaseLocation}")
            keyAlias "${ribotAppReleaseKeyAlias}"
            storePassword "${ribotAppReleaseStorePassword}"
            keyPassword "${ribotAppReleaseKeyPassword}"
        }

        debug {
            storeFile file("${ribotAppKeystoreDebugLocation}")
            keyAlias "${ribotAppDebugKeyAlias}"
            storePassword "${ribotAppDebugStorePassword}"
            keyPassword "${ribotAppDebugKeyPassword}"
        }
    }

    buildTypes {

        release {
            signingConfig signingConfigs.release

            /*
            * By default ProGuard optimizations are disabled. If you want to enable them
            * replace proguard-android.txt with proguard-android-optimize.txt but make sure you
            * test thoroughly if you go this route as app may become unstable.
            */
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            versionNameSuffix " Debug"
            debuggable true
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    //Needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        warning 'InvalidPackage'
    }

}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '24.1.0'
    final RETROFIT_VERSION = '2.0.0'
    final OKHTTP_VERSION = '3.2.0'
    final DAGGER_VERSION = '2.2'
    final BUTTERKNIFE_VERSION = '8.0.1'

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:percent:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"

    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"

    compile 'com.squareup.sqlbrite:sqlbrite:0.5.0'
    compile 'com.squareup:otto:1.3.8'

    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.estimote:sdk:0.9.4@aar'

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    apt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2
}

// Log out test results to console
tasks.matching {it instanceof Test}.all {
    testLogging.events = ["failed", "passed", "skipped"]
}